#!/bin/tcsh

set cdpath=( . \
    $SOURCE \
    $TEMP/builds )

if( $?tcsh ) then
	set autolist
	set autocorrect
	set autoexpand
	set matchbeep=nomatch
	set listjobs=long

	set noglob

	set hosts=(titans oceanus torsolca01 torsolca02 torsolca03 \
      torlcn01 torlcn02 torlcn03 torlcn04 torlcn05 torlcn06 hathor)

    complete ssh	n/*/\$hosts/	# argument from list in $hosts
    complete cd  	p/1/d/		# Directories only
    complete pushd 	p/1/d/
    complete popd 	p/1/d/
    complete mkdir 	p/1/d/
    complete rmdir 	p/1/d/
    complete complete 	p/1/c/
    complete uncomplete p/1/c/
    complete exec 	p/1/c/
    complete trace 	p/1/c/
    complete strace 	p/1/c/
    complete which 	p/1/c/
    complete where 	p/1/c/
    complete adb	p/1/c/ 
    complete gdb	p/1/c/
    complete ups	p/1/c/
    complete set	'c/*=/f/' 'p/1/s/=' 'n/=/f/'
    complete unset		p/1/s/
    complete setenv 	p/1/e/		# only environment variables 
    complete unsetenv 	p/1/e/
    complete alias 		p/1/a/		# only aliases are valid
    complete a		 	p/1/a/
    complete unalias 	p/1/a/
    complete rlogin 	c/-/"(l 8 e)"/ n/-l/u/ n/*/\$hosts/ 
    complete xrsh   	c/-/"(l 8 e)"/ n/-l/u/ n/*/\$hosts/ 
    complete gcc 	c/-I/d/ c/-L/d/ \
		 	c/-f/"(caller-saves cse-follow-jumps delayed-branch \
		               elide-constructors expensive-optimizations \
			       float-store force-addr force-mem inline \
			       inline-functions keep-inline-functions \
			       memoize-lookups no-default-inline \
			       no-defer-pop no-function-cse omit-frame-pointer \
			       rerun-cse-after-loop schedule-insns \
			       schedule-insns2 strength-reduce \
			       thread-jumps unroll-all-loops \
			       unroll-loops syntax-only all-virtual \
			       cond-mismatch dollars-in-identifiers \
			       enum-int-equiv no-asm no-builtin \
			       no-strict-prototype signed-bitfields \
			       signed-char this-is-variable unsigned-bitfields \
			       unsigned-char writable-strings call-saved-reg \
			       call-used-reg fixed-reg no-common \
			       no-gnu-binutils nonnull-objects \
			       pcc-struct-return pic PIC shared-data \
			       short-enums short-double volatile)"/ \
		 	c/-W/"(all aggregate-return cast-align cast-qual \
		      	       comment conversion enum-clash error format \
		      	       id-clash-len implicit missing-prototypes \
		      	       no-parentheses pointer-arith return-type shadow \
		      	       strict-prototypes switch uninitialized unused \
		      	       write-strings)"/ \
		 	c/-m/"(68000 68020 68881 bitfield fpa nobitfield rtd \
			       short c68000 c68020 soft-float g gnu unix fpu \
			       no-epilogue)"/ \
		 	c/-d/"(D M N)"/ \
		 	c/-/"(f W vspec v vpath ansi traditional \
			      traditional-cpp trigraphs pedantic x o l c g L \
			      I D U O O2 C E H B b V M MD MM i dynamic \
			      nodtdlib static nostdinc undef)"/ \
		 	c/-l/f:*.a/ \
		 	n/*/f:*.{c,C,cc,o,a}/
    complete g++ c/-I/d/ c/-L/d/ n/*/f:*.{C,cc,o}/ \
		 	c/-W/"(all aggregate-return cast-align cast-qual \
		      	       comment conversion enum-clash error format \
		      	       id-clash-len implicit missing-prototypes \
		      	       no-parentheses pointer-arith return-type shadow \
		      	       strict-prototypes switch uninitialized unused \
		      	       write-strings)"/
    complete CC 	n/*/f:*.{C,cc,o}/

    complete find 	n/-fstype/"(nfs 4.2)"/ n/-name/f/ \
		  	n/-type/"(c b d f p l s)"/ n/-user/u/ n/-exec/c/ \
		  	n/-ok/c/ n/-cpio/f/ n/-ncpio/f/ n/-newer/f/ \
		  	c/-/"(fstype name perm prune type user nouser \
		  	     group nogroup size inum atime mtime ctime exec \
			     ok print ls cpio ncpio newer xdev depth)"/ \
			n/*/d/

    complete kill	c/-/S/ c/%/j/
    complete %*		c/%/j/			# fill in the jobs builtin
    complete fg		c/%/j/
    complete bg		c/%/j/
    complete stop	c/%/j/

    complete limit	c/-/"(h)"/ n/*/l/
    complete unlimit	c/-/"(h)"/ n/*/l/

    complete zcat	n/*/f:*.Z/
    complete finger c/*@/\$hosts/ p/1/u/@ 

    complete talk p/1/'`users | tr " " "\012" | uniq`'/ \
		  n/*/\`who\ \|\ grep\ \$:1\ \|\ awk\ \'\{\ print\ \$2\ \}\'\`/

    if ( -f $HOME/.netrc ) then
	complete ftp    p@1@\`cat\ $HOME/.netrc\ \|\ \ awk\ \'\{\ print\ \$2\ \}\'\`@
    endif

    complete rcp c/*:/f/ C@[./]*@f@ n/*/\$hosts/:

    complete nm		n/*/f:^*.{h,C,c,cc}/
    complete ar c/[dmpqrtx]/"(c l o u v a b i)"/ p/1/"(d m p q r t x)"// \
		p/2/f:*.a/ p/*/f:*.o/

    complete dd c/if=/f/ c/of=/f/ \
		c/conv=*,/"(ascii ebcdic ibm block unblock \
			    lcase ucase swap noerror sync)"/,\
		c/conv=/"(ascii ebcdic ibm block unblock \
			  lcase ucase swap noerror sync)"/,\
	        c/*=/x:'<number>'/ \
		n/*/"(if of conv ibs obs bs cbs files skip file seek count)"/=

    complete mail	c/-/"(e i f n s u v)"/ c/*@/\$hosts/ \
			n/-s/x:'<subject>'/ n/-u/u/ \
			c@+@\`/bin/ls\ -1\ ~/Mail\`@ n/-f/f/ n/*/u/
    complete aseutil n/-o/"( \
      AddTriggers \
      BetaGeneration \
      CacheEstimation \
      CacheStatistics \
      ComponentInfo \
      ConfigValue \
      DataReport \
      ExportComponent \
      GetRiskFactorNames \
      GetScenarioSetNames \
      GetSessionDate \
      GetTriggers \
      ImportComponent \
      Interactive \
      MatrixGeneration \
      ModelParam \
      Ping \
      Query \
      Refresh \
      SaveAll \
      ScenarioComposition \
      ScenarioGeneration \
      ScenarioManifest \
      ScenarioUpdate \
      SetSessionDate \
      SetTriggers \
      ShutDown \
      SimulationMatrix)"/ \
      n/-r/"( \
      Binary \
      CSV \
      Excel \
      TiggerFilter \
      Manual \
      Algo00 Algo01 \
      Server00 Server01)"/ \
      n/-f/d/
	unset noglob
endif

# history auto-complete
bindkey -b ^p "history-search-backward"
bindkey -b ^n "history-search-forward"

# Other shell variables.
set notify set history=1000 savehist=1000
set mail=/usr/spool/mail/$USER

